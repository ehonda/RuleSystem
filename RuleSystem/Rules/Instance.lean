import Mathlib.Data.Finset.Powerset
import Mathlib.Data.Fintype.Basic
import RuleSystem.Rules.Defs
import RuleSystem.Rules.Fin
import RuleSystem.Rules.Finset

-- TODO: Instances for `hasMem` so we can just write `Fin.last _ âˆˆ inst` (instead of `Fin.last _ âˆˆ inst.tags`).
structure Instance (n : â„•) where
  tags : Tags n

namespace Instance

def untagged {n : â„•} : Instance n := âŸ¨âˆ…âŸ©

def tagsEmbedding {n : â„•} : Tags n â†ª Instance n := âŸ¨Instance.mk, by simp [Function.Injective]âŸ©

instance fintype {n : â„•} : Fintype (Instance n) :=
  let tagsUniv : Tags n := Finset.univ
  let tagsPowerset := tagsUniv.powerset
  let elems := tagsPowerset.map tagsEmbedding

  let complete : âˆ€ inst : Instance n, inst âˆˆ elems := by
    simp [Instance, elems, tagsEmbedding, tagsPowerset, tagsUniv]

  âŸ¨elems, completeâŸ©

-- TODO: Make ext work with instances @[ext]
theorem eq_iff_tags_eq {n : â„•} {inst inst' : Instance n} : inst = inst' â†” inst.tags = inst'.tags := by
  cases inst; cases inst'; simp

theorem eq_mk_iff_tags_eq {n : â„•} {tags : Tags n} {inst : Instance n}
  : inst = âŸ¨tagsâŸ© â†” inst.tags = tags := eq_iff_tags_eq

instance instDecidableEq {n : â„•} : DecidableEq (Instance n)
  := Î» _ _ â†¦ decidable_of_iff' _ eq_iff_tags_eq

instance instDecidableTagsNonempty {n : â„•} (inst : Instance n) : Decidable (inst.tags.Nonempty)
  := Finset.decidableNonempty

def castSucc {n : â„•} (inst : Instance n) : Instance (n + 1) := âŸ¨Finset.castSucc inst.tagsâŸ©

def castSuccEmbedding {n : â„•} : Instance n â†ª Instance (n + 1) :=
  âŸ¨castSucc, by simp [Function.Injective, castSucc, Finset.castSucc, eq_iff_tags_eq]âŸ©

-- This should be useful fairly often when working with `Instance.castSucc` and `Rule.castSucc`, with regards to their
-- commutativity. Whether or not instances with `last` are captured should often play a crucial role in proofs.
--
-- TODO: â“ Should we do this or not? Investigate common use cases first.
-- We make `inst` and `inst'` explicit rather than the `of_`-arguments here, because the common use case should be via
-- `apply Instance.false_of_last_mem_of_castSuccEmbedding_eq inst inst'` to then generate sub goals for the
-- `of_`-arguments. Otherwise we would have to resort to the longer and harder to understand
-- `@Instance.false_of_last_mem_of_castSuccEmbedding_eq _ inst inst' _ _`.
theorem false_of_last_mem_of_castSuccEmbedding_eq
    {n : â„•}
    {inst : Instance n}
    {inst' : Instance (n + 1)}
    (last_mem_inst' : Fin.last _ âˆˆ inst'.tags)
    (inst_castSuccEmbedding_eq_inst' : inst' = (inst |> castSuccEmbedding))
  : False := by
    simp [inst_castSuccEmbedding_eq_inst', castSuccEmbedding, castSucc, Finset.castSucc] at last_mem_inst'
    obtain âŸ¨_, _, _âŸ© := last_mem_inst'
    apply Fin.false_of_castLE_eq_last
    assumption

-- TODO: ğŸ•µï¸â€â™€ï¸ Revisit! The whole block about `castPred` is just copied from `TheoremsAboutAlgorithms` and works, but we
--        should wrap our head around it once more. See:
--          * https://github.com/ehonda/TheoremsAboutAlgorithms/blob/a8d8a946f0e34dd987996f1f7f209bf61a598a72/TheoremsAboutAlgorithms/Partitions/WithFinset/Cell.lean#L122-L123

def CastPredPrecondition {n : â„•} (inst : Instance (n + 1)) := âˆ€ tag âˆˆ inst.tags, tag â‰  Fin.last _

-- We're using `Subtype.restrict` here. Revisit this to fully understand what's going on here.
def restrictFinCastPred {n : â„•} (inst : Instance (n + 1)) (h : inst.CastPredPrecondition) (tag : inst.tags) : Fin n
  := tag.restrict (Â· âˆˆ inst.tags) Fin.castPred (h tag tag.property)

theorem restrictFinCastPred_injective {n : â„•} (inst : Instance (n + 1)) (h : inst.CastPredPrecondition)
  : Function.Injective (restrictFinCastPred inst h) := by
    intro x y castPred_x_eq_castPred_y
    simp [restrictFinCastPred] at castPred_x_eq_castPred_y
    apply Subtype.eq
    exact Fin.castPred_inj.mp castPred_x_eq_castPred_y

def castPred {n : â„•} (inst : Instance (n + 1)) (h : inst.CastPredPrecondition) : Instance n :=
  âŸ¨Finset.univ.map âŸ¨inst.restrictFinCastPred h, restrictFinCastPred_injective inst hâŸ©âŸ©

theorem castPred_castSucc_eq {n : â„•} {inst : Instance (n + 1)} (h : inst.CastPredPrecondition)
  : (inst.castPred h).castSucc = inst := by
    simp [eq_iff_tags_eq, castSucc, Finset.castSucc, castPred]
    ext tag
    constructor
    Â· intro tag_mem_castPred_castSucc
      simp [Fin.castSuccEmb, restrictFinCastPred, Subtype.restrict] at tag_mem_castPred_castSucc
      obtain âŸ¨tag', âŸ¨_, _, _âŸ©, _âŸ© := tag_mem_castPred_castSucc
      subst tag tag'
      simpa [Fin.castPred, Fin.castLE]
    Â· intro tag_mem_inst_tags
      simp [Fin.castSuccEmb, restrictFinCastPred, Subtype.restrict]
      have tag_ne_last : tag â‰  Fin.last _ := h tag tag_mem_inst_tags
      set tag' := Fin.castPred tag tag_ne_last with tag'_def
      exists tag'
      constructor
      Â· exists tag
        exists tag_mem_inst_tags
      Â· simp [tag'_def, Fin.castPred, Fin.castLE]

theorem castPred_castSuccEmbedding_eq {n : â„•} {inst : Instance (n + 1)} (h : inst.CastPredPrecondition)
  : (inst.castPred h |> castSuccEmbedding) = inst := by
    simp [castSuccEmbedding, castPred_castSucc_eq]

abbrev HasCastPredPrecondition (n : â„•)
  := Subtype (Î» (inst : Instance (n + 1)) â†¦ inst.tags.CastPredPrecondition)

end Instance

abbrev Instances (n : â„•) := Finset (Instance n)

namespace Instances

def castSucc {n : â„•} : Instances n â†’ Instances (n + 1) := Finset.map Instance.castSuccEmbedding

-- TODO: Why can't we write this with the fancy notation?
def containingLast {n : â„•} : Instances (n + 1) := Finset.univ.filter (Î» inst â†¦ Fin.last _ âˆˆ inst.tags)

def notContainingLast {n : â„•} : Instances (n + 1) := Finset.univ.filter (Î» inst â†¦ Fin.last _ âˆ‰ inst.tags)

theorem val_tags_CastPredPrecondition_of_notContainingLast
    {n : â„•}
    (inst : @notContainingLast n)
  : inst.val.tags.CastPredPrecondition := by
    -- TODO: This is a bit awkward, find a cleaner proof
    simp [Finset.CastPredPrecondition]
    have := inst.property
    simp only [notContainingLast, Finset.mem_filter] at this
    obtain âŸ¨_, _âŸ© := this
    assumption

instance CoeNotContainingLastHasCastPredPrecondition {n : â„•}
  : Coe (@Instances.notContainingLast n) (@Instance.HasCastPredPrecondition n) where
    coe := by
      intro inst
      exists inst
      apply val_tags_CastPredPrecondition_of_notContainingLast

-- TODO: Naming
-- TODO: Use the more general version instead
--
-- Compared to
--
--    `inter_eq_empty_iff_castSucc_inter_castSucc_eq_empty : s âˆ© t = âˆ… â†” s.map Fin.castSuccEmb âˆ© t.map Fin.castSuccEmb = âˆ…`
--
-- which, denoting `map castSuccEmb` as `â†‘` and `map castPredEmb` as `â†“`, is `x âˆ© y = âˆ… â†” xâ†‘ âˆ© yâ†‘ = âˆ…`, we here have
-- `x âˆ© yâ†“ = âˆ… â†” xâ†‘ âˆ© y = âˆ…`.
theorem inter_eq_empty_iff_inter_map_castSuccEmb_left_eq_empty_of_castPred
    {n : â„•}
    {tags : Tags n}
    {inst' : Instance n}
    {inst : Instance (n + 1)}
    (inst_castPredPrecondition : inst.CastPredPrecondition)
    (inst'_eq_inst_castPred : inst' = inst.castPred inst_castPredPrecondition)
  : tags âˆ© inst'.tags = âˆ… â†” tags.map Fin.castSuccEmb âˆ© inst.tags = âˆ… := by
    subst inst'
    apply Finset.inter_castPred_eq_empty_iff_castSucc_inter_eq_empty inst_castPredPrecondition

end Instances

------------------------------------------------------------------------------------------------------------------------
--                                                   ADVANCED                                                         --
------------------------------------------------------------------------------------------------------------------------

-- TODO: Better structure of this file

namespace Instance

-- TODO: Naming
-- TODO: Use `insert`
def insertLast' {n : â„•} (inst : @Instances.notContainingLast n) : Instance (n + 1)
  := âŸ¨inst.val.tags âˆª {Fin.last _}âŸ©

theorem insertLast'_injective {n : â„•} : Function.Injective (@insertLast' n) := by
  intro x y insertLast_x_eq_insertLast_y
  ext
  apply eq_iff_tags_eq.mpr
  ext tag
  simp [insertLast'] at insertLast_x_eq_insertLast_y
  constructor
  -- TODO: Get rid of the symmetric case
  Â· intro tag_mem_insertLast_x
    cases Decidable.eq_or_ne tag (Fin.last _) with
      | inl tag_eq_last =>
        have := Instances.val_tags_CastPredPrecondition_of_notContainingLast x tag tag_mem_insertLast_x
        contradiction
      | inr tag_ne_last =>
        have : tag âˆˆ y.val.tags âˆª {Fin.last _} := by
          have := Finset.mem_union_left {Fin.last _} tag_mem_insertLast_x
          rwa [insertLast_x_eq_insertLast_y] at this
        simp [tag_ne_last] at this
        assumption
  Â· intro tag_mem_insertLast_y
    cases Decidable.eq_or_ne tag (Fin.last _) with
      | inl tag_eq_last =>
        have := Instances.val_tags_CastPredPrecondition_of_notContainingLast y tag tag_mem_insertLast_y
        contradiction
      | inr tag_ne_last =>
        have : tag âˆˆ x.val.tags âˆª {Fin.last _} := by
          have := Finset.mem_union_left {Fin.last _} tag_mem_insertLast_y
          rw [â† insertLast_x_eq_insertLast_y] at this
          assumption
        simp [tag_ne_last] at this
        assumption

def insertLast'_embedding {n : â„•} : @Instances.notContainingLast n â†ª Instance (n + 1) :=
  âŸ¨insertLast', insertLast'_injectiveâŸ©

end Instance

namespace Instances

def insertLast' {n : â„•} (insts : Finset (@notContainingLast n)) : Instances (n + 1)
  := insts.map Instance.insertLast'_embedding

-- TODO: Can we use this definition?
-- def insertLast' {n : â„•} (insts : Instance.HasCastPredPrecondition n) : Instances (n + 1)
--   := insts.map Instance.insertLast'_embedding

end Instances
